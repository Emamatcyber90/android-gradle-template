buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // support to get app module under test with robolectric in separated test module
        classpath "com.novoda:gradle-android-test-plugin:0.10.4"
    }
}

apply plugin: 'java'

// support to get app module under test with robolectric in separated test module
apply plugin: 'android-test'

android {
    projectUnderTest ':app'
}

apply from: 'build-novoda-android-studio.gradle'

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile('com.squareup.assertj:assertj-android:1.1.0') {
        exclude module: "support-annotations"
    }
    testCompile 'org.mockito:mockito-all:2.0.2-beta'

    testCompile('org.robolectric:shadows-support-v4:3.0') {
        exclude module: "robolectric"
        exclude module: "support-v4"
        exclude module: "support-annotations"
    }

    testCompile('org.robolectric:robolectric:3.0') {
        exclude module: 'classworlds'
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
        exclude module: 'maven-artifact'
        exclude module: 'maven-artifact-manager'
        exclude module: 'maven-error-diagnostics'
        exclude module: 'maven-model'
        exclude module: 'maven-project'
        exclude module: 'maven-settings'
        exclude module: 'plexus-container-default'
        exclude module: 'plexus-interpolation'
        exclude module: 'plexus-utils'
        exclude module: 'wagon-file'
        exclude module: 'wagon-http-lightweight'
        exclude module: 'wagon-provider-api'
    }
}


// force utf-8 is necessary for windows environments, without it test classes aren't interpreted in utf-8
tasks.withType(JavaCompile) {
    options.encoding = "utf-8"
}

afterEvaluate {

    def isLintRun = false
    def isTestRun = false

    gradle.startParameter.taskNames.each {
        if (it.contains("lint")) {
            isLintRun = true
        }
        if (it.contains("test")) {
            isTestRun = true
        }
    }

    if (isLintRun && isTestRun) {
        println "WARNING: tests for release type are disabled for supporting jacoco"
        println "WARNING: run test and lint at same time is not supported"
        exit 1
    }

    if (isTestRun) {
        tasks.each {
            if (it.name.contains("Release")) {
                it.enabled = false
            }
        }
    }
}