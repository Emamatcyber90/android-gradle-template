apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

/** Without "includeNoLocationClasses" we would get zero coverage report when using robolectric. */
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

/** We must create our own task to create jacoco reports. */
task jacocoInstrumentationTestReport(type: JacocoReport, dependsOn: ['connectedCheck']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*' ]

    def debugTreeApp = fileTree(dir: "$project.rootDir/app/build/tmp/kotlin-classes/debug", excludes: fileFilter)
    def debugTreeDataLocal = fileTree(dir: "$project.rootDir/data-local/build/tmp/kotlin-classes/debug", excludes: fileFilter)
    def debugTreeDataNetwork = fileTree(dir: "$project.rootDir/data-network/build/tmp/kotlin-classes/debug", excludes: fileFilter)

    def mainSrcApp = "$project.rootDir/app/src/main/java"
    def mainSrcDataLocal = "$project.rootDir/data-local/src/main/java"
    def mainSrcDataNetwork = "$project.rootDir/data-network/src/main/java"

    sourceDirectories = files([mainSrcApp, mainSrcDataLocal, mainSrcDataNetwork])
    classDirectories = files([debugTreeApp, debugTreeDataLocal, debugTreeDataNetwork])
    executionData = fileTree(dir: project.buildDir, includes: [
            'outputs/code-coverage/connected/*coverage.ec'
    ])
}