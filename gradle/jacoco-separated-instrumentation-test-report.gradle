apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.8.2"
}

/** Without "includeNoLocationClasses" we would get zero coverage report when using robolectric. */
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

/** We must create our own task to collect all project sources, without we had nothing to cover. */
task jacocoInstrumentationTestReport(type: JacocoReport) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*\$*']
    def debugTree = fileTree(dir: "$project.rootDir", excludes: fileFilter).matching {
        include "**/build/tmp/kotlin-classes/debug/**"
    }
    def mainSrc = []
    new File("$project.rootDir").eachDirRecurse() { dir ->
        if(dir.path.matches(/.*main.java/)) mainSrc.add(dir)
    }

    sourceDirectories = files(mainSrc)
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'outputs/code_coverage/debug/connected/*coverage.ec'
    ])
}
