apply plugin: 'com.android.application'
// kotlin android - enables kotlin support
apply plugin: 'kotlin-android'
// kotlin android extensions - many helpful tools to avoid boilerplate code
apply plugin: 'kotlin-android-extensions'
// kotlin kapt - enables code generation
apply plugin: "kotlin-kapt"
// kotlin allopen - removes the default final classifier for AndroidAnnotations
apply plugin: "kotlin-allopen"
apply from: "$project.rootDir/gradle/jacoco-unit-test-report.gradle"
apply from: "$project.rootDir/gradle/jacoco-instrumentation-test-report.gradle"

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.template.project"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "0.0.1"
        testInstrumentationRunner "com.template.project.tools.AndroidJUnitRunnerCustomApplication"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    kotlin {
        experimental {
            // Could be removed after Channel support is not anymore experimental.
            coroutines 'enable'
        }
    }
}

dependencies {
    // --------------------------------------------------
    // Project modules
    //
    implementation project(":data-local")
    implementation project(":data-network")

    // --------------------------------------------------
    // Kotlin support
    //
    // - for standard Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // - for async execution with coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.0.0"

    // --------------------------------------------------
    // Backward compatibility
    //
    api "androidx.appcompat:appcompat:1.0.2"

    // --------------------------------------------------
    // Design elements
    //
    // - for ConstraintLayout
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    // - for RecyclerView
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    // - for TextInputLayout
    implementation "com.google.android.material:material:1.0.0"

    // --------------------------------------------------
    // Code architecture
    //
    // - for LiveData, ViewModel
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    // - for Navigation components
    api "android.arch.navigation:navigation-fragment-ktx:1.0.0-alpha07"

    // --------------------------------------------------
    // Reduce boilerplate code
    //
    // - for view enhancement (and optional many more)
    kapt "org.androidannotations:androidannotations:4.6.0"
    api "org.androidannotations:androidannotations-api:4.6.0"

    // --------------------------------------------------
    // Dependency injection
    //
    // - for Koin on Android
    implementation 'org.koin:koin-android:1.0.2'
    // - for Koin Android ViewModel support
    implementation 'org.koin:koin-androidx-viewmodel:1.0.2'

    // --------------------------------------------------
    // Unit Test (jvm)
    //
    testImplementation "junit:junit:4.12"
    testImplementation "com.google.truth:truth:0.42"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    testImplementation "androidx.arch.core:core-testing:2.0.0"
    testImplementation "org.mockito:mockito-core:2.23.4"

    // --------------------------------------------------
    // Tests (instrumentation)
    //
    androidTestImplementation "androidx.test:runner:1.1.0"
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation "androidx.test.ext:junit:1.0.0"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.0"
    androidTestImplementation "com.google.truth:truth:0.42"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0"
    androidTestImplementation 'org.mockito:mockito-android:2.23.0'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
}

allOpen {
    // removes the default final classifier for AndroidAnnotations when it is necessary
    annotation("org.androidannotations.api.KotlinOpen")
}
