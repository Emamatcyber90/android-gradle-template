buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // code generation support for android annotations
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    }
}

apply plugin: 'com.android.application'

// code generation support for android annotations
apply plugin: 'android-apt'

// test support for android specific unit tests like database checks
apply from: 'build.robolectric.gradle'

// support code coverage for the unit tests
apply from: 'build.jacoco-test-report.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        applicationId "com.example.app"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "MODULE_PATH", "\"$projectDir\"".replace('\\', '\\\\')
            // avoid warning for "Not all execution paths return a value"
            return true
        }
        debug {
            buildConfigField "String", "MODULE_PATH", "\"$projectDir\"".replace('\\', '\\\\')
            return true
        }
    }

    lintOptions {
        // GradleDependency Current we can't switch to support v23 tools because of robolectric
        disable 'GradleDependency'
    }

    dexOptions {
        if(isCi) {
            // CircleCi allow max 4G memory for all processes together and dex does exceed it
            // with his parallel execution. Shrink the javaMaxHeapSize does it only per dex process
            // but the combination exceed the max memory and to less value let it run for long time.
            // javaMaxHeapSize "1024M"

            // Speed up build for CI by ignoring extra build steps which should speed up build for developers.
            // Also reduce memory usage by avoiding multiple (pre) dex processes.
            incremental false
            preDexLibraries = false
        }
    }

    // instrumentation test module need to access the expected variant, default is only release publish
    publishNonDefault true

    packagingOptions {
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs.collect()[0]?.processResources?.manifestFile
    }
}

repositories {
    jcenter()
}

dependencies {
    // ui design and backward compatibility
    compile "com.android.support:appcompat-v7:23.3.0"
    compile "com.android.support:design:23.3.0"

    compile(project(':core')) {
        exclude module: "design"
    }

    // android annotations support
    apt "org.androidannotations:androidannotations:4.0.0"
    compile "org.androidannotations:androidannotations-api:4.0.0"

    // Use CountIdlingResource class to avoid flaky espresso tests.
    // Interface is provided by own dependency but ready to use implementation comes with contrib library.
    // Looks like contrib library was only intended to be used within test scope.
    // Exclude transitiv dependencies to avoid conflicts with test support libraries.
    compile('com.android.support.test.espresso:espresso-idling-resource:2.2')
    compile('com.android.support.test.espresso:espresso-contrib:2.2') { exclude module: "*" }

    // basic unit tests support + mocks + fluent assertions + android assertions
    testCompile 'junit:junit:4.12'
    testCompile('com.squareup.assertj:assertj-android:1.1.0') {
        exclude module: 'support-annotations' // Avoid Error:Conflict ... Resolved versions for app (X) and test app (Y) differ ...
    }
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

