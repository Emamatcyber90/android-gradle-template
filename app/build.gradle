buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        // code generation support for android annotations
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'

        // code generation for json string - object mapping
        classpath 'org.jsonschema2pojo:jsonschema2pojo-gradle-plugin:0.4.14'
    }
}

plugins {
    // provide download task for gradle
    id "de.undercouch.download" version "1.2"
}

apply plugin: 'com.android.application'

// code generation support for android annotations
apply plugin: 'android-apt'

// code generation for json object mapping
apply from: 'build-generate-json-objects.gradle'

// code generation database files
apply from: 'build-generate-database.gradle'

// test support for android specific unit tests like database checks
apply from: 'build-robolectric.gradle'

android {
    compileSdkVersion projectAndroidVersion
    buildToolsVersion projectAndroidBuildToolsVersion

    defaultConfig {
        applicationId "com.example.project"
        minSdkVersion projectAndroidMinVersion
        targetSdkVersion projectAndroidVersion
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "MODULE_PATH", "\"$projectDir\"".replace('\\', '\\\\')
        }
        debug {
            buildConfigField "String", "MODULE_PATH", "\"$projectDir\"".replace('\\', '\\\\')
        }
    }

    // instrumentation test module need to access the expected variant, default is only release
    publishNonDefault true

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

apt {
    arguments {
        androidManifestFile variant.outputs.collect()[0].processResources.manifestFile
    }
}

repositories {
    jcenter()
}

dependencies {
    // ui design and backward compatibility
    compile "com.android.support:appcompat-v7:$projectAndroidSupportToolsVersion"
    compile "com.android.support:design:$projectAndroidSupportToolsVersion"

    // android annotations support
    apt "org.androidannotations:androidannotations:3.3.2"
    compile "org.androidannotations:androidannotations-api:3.3.2"

    // better time handling with DateTime
    compile 'net.danlew:android.joda:2.8.2'

    // java language tools like StringUtils
    compile 'org.apache.commons:commons-lang3:3.4'

    // force test dependencies to use the same support-annotations version
    testCompile "com.android.support:support-annotations:$projectAndroidSupportToolsVersion"

    // basic unit tests support + mocks + fluent assertions + android assertions
    testCompile 'junit:junit:4.12'
    testCompile 'com.squareup.assertj:assertj-android:1.1.0'
    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}